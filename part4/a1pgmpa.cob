000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.      A1PGMPA.
000300
000400 ENVIRONMENT DIVISION.
000500
000600
000700 DATA DIVISION.
000800 WORKING-STORAGE SECTION.
000900     COPY DFHBMSCA.
001000     COPY DFHAID.
001100     COPY MS1PA.
001200
001300 01 ZONE.
001400   05 COMMA-PGM  PIC X(8) VALUE 'A1PGMPA'.
001410   05 FILLER     PIC X(4).
001500   05 INFOS      PIC X(50).
001510
001520*************************************************************
001530* VARIABLE POUR LES INFORMATIONS GENERALE DE LA MAP
001540*************************************************************
001610 77 WS-TEMPS     PIC S9(15) COMP-3.
001700 77 WS-CD-ERR    PIC 99.
001710 77 WS-ERR-MESS  PIC X(50) VALUE 'FIN NORMALE'.
001720
001730 77 WS-RES-OPE   PIC ZZBZZ9.99.
001740
001750 77 WS-MSG       PIC X(50).
001760
001770*************************************************************
001780* VARIABLE POUR LES INFORMATIONS DE LA PIECE
001790*************************************************************
001812 01 ENR-PART.
001813   02 CODE-PART     PIC   X(2)           .
001814   02 NAME-PART     PIC   X(30)          .
001815   02 COLOR-PART    PIC   X(20)          .
001816   02 WEIGHT-PART   PIC   9(2)           .
001817   02 CITY-PART     PIC   X(20)          .
001819   02 FILLER        PIC   X(6)           .
001820
001821* VARIABLES POUR VERIFIER LA SAISIE
001822 01 FLAG   PIC 9.
001823    88 ALL-ZONES-OK VALUE 0.
001824    88 MANQUE-ZONE  VALUE 1.
001825
001826 77 UNDER-SCORE PIC X(79) VALUE ALL '_'.
001830
001900 PROCEDURE DIVISION.
002000
002100     EVALUATE EIBTRNID
002200         WHEN 'T1LG'
002310* PREMIERE FOIS QU'ON AFFICHE LA MAP (EIBCALEN = 0) *
002400              IF EIBCALEN = ZERO
002401                     MOVE LOW-VALUE TO MAP1PAO
002410                     PERFORM ENVOI-ECRAN
002420              ELSE
002421                     PERFORM GESTION-TOUCHES
002423              END-IF
002500         WHEN OTHER
002600              MOVE LOW-VALUE TO MAP1PAO
002700     END-EVALUATE
002800     PERFORM ENVOI-ECRAN
002810     GOBACK.
002900
003000***************************************************************
003100*            LISTE DES PARAGRAPHES                            *
003110***************************************************************
003111
003120***************************************************************
003130* PARAGRAPHE POUR L'ENVOIE D'INFORMATION A L'ECRAN            *
003140***************************************************************
003200 ENVOI-ECRAN.
003210
003500      MOVE 'AJOUT PART ' TO LIBMAPO
003501
003510      EXEC CICS ASKTIME
003520          ABSTIME (WS-TEMPS)
003530      END-EXEC
003540
003550      EXEC CICS
003560         FORMATTIME ABSTIME (WS-TEMPS)
003570         DDMMYY (DATEJO)
003580         DATESEP('/')
003590         TIME (HEUREO)
003591         TIMESEP(':')
003592      END-EXEC
003596
003600      EXEC CICS
003700         SEND MAP('MAP1PA')
003800              MAPSET('MS1PA')
003801              FROM(MAP1PAO)
003810              RESP(WS-CD-ERR)
003900              ERASE
004000              CURSOR
004100      END-EXEC
004200      IF WS-CD-ERR NOT EQUAL DFHRESP(NORMAL)
004210                 MOVE 'ERR SEND' TO WS-ERR-MESS
004300                 PERFORM FIN-TOTALE
004400      END-IF
004410*************************************************************
004420*    REAFFICHER LA TRANSACTION T1PA,
004421*          REDONNER LA MAIN A L'USER POUR VISUALISER LA MAP
004422*          ET EFFECTUER LES SAISIES
004430*************************************************************
004440      EXEC CICS
004450         RETURN TRANSID('T1PA')
004460         COMMAERA (ZONE)
004470         LENGTH (LENGTH OF ZONE)
004480      END-EXEC
004481      EXIT.
004490
004492
004493 LECT-ECRAN.
004500      EXEC CICS
004510           RECEIVE MAP('MAP1PA')
004520                   MAPSET('MS1PA')
004530                   RESP(WS-CD-ERR)
004540      END-EXEC
004541      IF WS-CD-ERR NOT EQUAL DFHRESP(NORMAL)
004542                 MOVE 'ERR RECE' TO WS-ERR-MESS
004543                 PERFORM FIN-TOTALE
004544      END-IF
004545      EXIT.
004546
004547******************************************************************
004548* PARAGRAPHE POUR VERIFIER SI LA PIECE EXISTE DEJA OU PAS        *
004549******************************************************************
004550 LECTURE-PART.
004551      EXEC CICS
004552         READ DATASET('PARTS1')
004553              INTO(ENR-PART)
004554              RIDFLD(CODEPAI)
004555              RESP(WS-CD-ERR)
004556      END-EXEC
004557      EVALUATE WS-CD-ERR
004558         WHEN ZERO
004559            MOVE 'CODE PART EXISTE DEJA' TO MESS1O
004560* PERMET DE METTRE EN ROUGE LE CHAMP
004561            MOVE DFHRED TO CODEPAC
004562* PERMET DE METTRE LE CURSOR SUR LE CHAMP
004563            MOVE -1     TO CODEPAL
004564            PERFORM ENVOI-ECRAN
004565         WHEN 13
004566            CONTINUE
004567         WHEN OTHER
004568            MOVE 'ERREUR READ PARTS1' TO WS-ERR-MESS
004569            PERFORM FIN-TOTALE
004570      END-EVALUATE
004571      EXIT.
004572
004573
004574******************************************************************
004575* PARAGRAPHE POUR AJOUTER LA PIECE SI ELLE N'EXISTE PAS          *
004576******************************************************************
004577 AJOUT-PART.
004578      IF ALL-ZONES-OK
004579         MOVE CODEPAI  TO CODE-PART
004580         MOVE NOMPAI   TO NAME-PART
004581         MOVE COLPAI   TO COLOR-PART
004582         MOVE WEIPAI   TO WEIGHT-PART
004583         MOVE CITYPAI  TO CITY-PART
004584
004585         EXEC CICS
004586          WRITE DATASET('PARTS1')
004587                FROM(ENR-PART)
004588                RIDFLD(CODE-PART)
004589                RESP(WS-CD-ERR)
004590         END-EXEC
004591         EVALUATE WS-CD-ERR
004592            WHEN ZERO
004593               MOVE 'AJOUT EFFECTUE ' TO MESS1O
004594               MOVE DFHBLUE TO CODEPAC
004595               PERFORM ENVOI-ECRAN
004596            WHEN 14
004597               MOVE 'DOUBLON SUR ' TO MESS1I
004598               MOVE CODEPAI        TO MESS2I
004599               PERFORM ENVOI-ECRAN
004600            WHEN OTHER
004601               MOVE 'ERREUR WRITE PARTS1' TO WS-ERR-MESS
004602               PERFORM FIN-TOTALE
004603         END-EVALUATE
004604
004605      END-IF
004606      EXIT.
004607
004608******************************************************************
004609* PARAGRAPHE POUR VERIFIER SI LES CHAMPS SONT REMPLIS OU NON     *
004610******************************************************************
004611 VERIF-SAISIE.
004612      INSPECT NOMPAI  REPLACING ALL '_' BY SPACES
004613      INSPECT COLPAI  REPLACING ALL '_' BY SPACES
004614      INSPECT WEIPAI  REPLACING ALL '_' BY SPACES
004615      INSPECT CITYPAI REPLACING ALL '_' BY SPACES
004616      SET ALL-ZONES-OK TO TRUE
004617
004618      IF NOMPAI = SPACES
004619         SET MANQUE-ZONE TO TRUE
004620         MOVE DFHRED TO NOMPAC
004621         MOVE UNDER-SCORE TO NOMPAI
004622      END-IF
004623      IF COLPAI = SPACES
004624         SET MANQUE-ZONE TO TRUE
004625         MOVE DFHRED TO COLPAC
004626         MOVE UNDER-SCORE TO COLPAI
004627      END-IF
004628      IF WEIPAI = SPACES
004629         SET MANQUE-ZONE TO TRUE
004630         MOVE DFHRED TO WEIPAC
004631         MOVE UNDER-SCORE TO WEIPAI
004632      END-IF
004633      IF CITYPAI = SPACES
004634         SET MANQUE-ZONE TO TRUE
004635         MOVE DFHRED TO CITYPAC
004636         MOVE UNDER-SCORE TO CITYPAI
004637      END-IF
004638      EXIT.
004639
004640******************************************************************
004641* PARAGRAPHE POUR LIRE LES CHAMPS ET VERIFIER SI LA PIECE EXISTE *
004642* ET L'AJOUTER SI TOUT EST CORRECTE                              *
004643******************************************************************
004644 TRAIT-SAISIE.
004645      INITIALIZE MESS1O MESS2O
004646
004647      PERFORM LECTURE-PART
004648      PERFORM VERIF-SAISIE
004649      PERFORM AJOUT-PART
004650      EXIT.
004651
004652******************************************************************
004653* PARAGRAPHE POUR GERER LES CAS SELON LA TOUCHE PRESSEE          *
004654******************************************************************
004655 GESTION-TOUCHES.
004656      INITIALIZE WS-MSG
004657      EVALUATE TRUE
004658      WHEN EIBAID = DFHCLEAR
004659             MOVE 'BYE' TO WS-ERR-MESS
004660             PERFORM FIN-TOTALE
004661      WHEN EIBAID = DFHENTER
004662             PERFORM LECT-ECRAN
004663             PERFORM TRAIT-SAISIE
004664      WHEN OTHER
004665             INITIALIZE MESS1O MESS2O
004666             MOVE 'TOUCHE INVALIDE !' TO MESS1O
004667             MOVE DFHPINK TO MESS1C
004668      END-EVALUATE
004669      EXIT.
004670
004671******************************************************************
004672* PARAGRAPHE POUR QUITTER L'AFFICHAGE DE LA MAP                  *
004673******************************************************************
004680 FIN-TOTALE.
004700      EXEC CICS
004800         SEND FROM (WS-ERR-MESS)
004900              LENGTH (LENGTH OF WS-ERR-MESS)
005000              WAIT
005100              ERASE
005200      END-EXEC
005300      EXEC CICS      RETURN       END-EXEC.
